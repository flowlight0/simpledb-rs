use std::str::FromStr;
use super::statement;
use super::predicate;
use crate::record::field::{Spec, Value};

grammar;

match {
    r"(?i)SELECT" => SELECT,
    r"(?i)FROM" => FROM,
    r"(?i)WHERE" => WHERE,
    r"(?i)INSERT" => INSERT,
    r"(?i)INTO" => INTO,
    r"(?i)VALUES" => VALUES,
    r"(?i)DELETE" => DELETE,
    r"(?i)MODIFY" => MODIFY,
    r"(?i)SET" => SET,
    r"(?i)CREATE" => CREATE,
    r"(?i)TABLE" => TABLE,
    r"(?i)VIEW" => VIEW,
    r"(?i)INDEX" => INDEX,
    r"(?i)ON" => ON,
    r"(?i)AS" => AS,
    r"(?i)AND" => AND,
    r"(?i)NULL" => NULL,
    r"(?i)I32" => I32,
    r"(?i)VARCHAR" => VARCHAR,
} else {
    "(" => LPAREN,
    ")" => RPAREN,
    "," => COMMA,
    "=" => EQ,
} else {
    r"-?[0-9]+" => I32_LITERAL,
    r"'[^']+'" => STRING_LITERAL,
} else {
    r"[a-zA-Z][a-zA-Z0-9_]*" => ID,
} else {
    r"[ \t\n]+" => WHITESPACE,
}

pub Statement: statement::Statement = {
    <q:Query> => statement::Statement::Query(q),
    <c:UpdateCommand> => statement::Statement::UpdateCommand(c),
}

pub Query: statement::QueryData = {
    SELECT <fs:Comma<IdToken>> FROM <ts:Comma<IdToken>> WHERE <p:Predicate> => statement::QueryData::new(fs, ts, Some(p)),
    SELECT <fs:Comma<IdToken>> FROM <ts:Comma<IdToken>> => statement::QueryData::new(fs, ts, None),
}

pub UpdateCommand: statement::UpdateCommand = {
    INSERT INTO <t:IdToken> LPAREN <fs:Comma<IdToken>> RPAREN VALUES LPAREN <vs:Comma<Constant>> RPAREN => statement::UpdateCommand::Insert(t, fs, vs),
    DELETE FROM <t:IdToken> => statement::UpdateCommand::Delete(t, None),
    DELETE FROM <t:IdToken> WHERE <p:Predicate> => statement::UpdateCommand::Delete(t, Some(p)),
    MODIFY <t:IdToken> SET <f:IdToken> EQ <e:Expression> => statement::UpdateCommand::Modify(t, f, e, None),
    MODIFY <t:IdToken> SET <f:IdToken> EQ <e:Expression> WHERE <p:Predicate> => statement::UpdateCommand::Modify(t, f, e, Some(p)),
    <c:CreateCommand> => statement::UpdateCommand::Create(c),
}

CreateCommand: statement::CreateCommand = {
    CREATE TABLE <t:IdToken> LPAREN <ds:Comma<FieldDefinition>> RPAREN => statement::CreateCommand::Table(t, ds),
    CREATE VIEW <t:IdToken> AS <q:Query> => statement::CreateCommand::View(t, q),
    CREATE INDEX <t:IdToken> ON <n:IdToken> LPAREN <f:IdToken> RPAREN => statement::CreateCommand::Index(t, n, f),
}

FieldDefinition: statement::FieldDefinition = {
    <n:IdToken> <t:FieldType> => statement::FieldDefinition::new(n, t),
}

FieldType: Spec = {
    I32 => Spec::I32,
    VARCHAR LPAREN <i:I32Constant> RPAREN => Spec::VarChar(i as usize),
}

pub Predicate: predicate::Predicate = {
    <ts:And<Term>> => predicate::Predicate::new(ts),
}

pub Term: predicate::Term = {
    <l:Expression> EQ <r:Expression> => predicate::Term::Equality(l, r),
}

Expression: predicate::Expression = {
    NULL => predicate::Expression::NullConstant,
    <i:I32Constant> => predicate::Expression::I32Constant(i),
    <s:StringConstant> => predicate::Expression::StringConstant(s),
    <t:IdToken> => predicate::Expression::Field(t),
}

Constant: Value = {
    NULL => Value::Null,
    <i:I32Constant> => Value::I32(i),
    <s:StringConstant> => Value::String(s),
}

I32Constant: i32 = {
    I32_LITERAL => i32::from_str(<>).unwrap(),
}

StringConstant: String = {
    STRING_LITERAL => String::from(&<>[1..<>.len()-1].to_string()),
}

IdToken: String = {
    ID => String::from(<>),
}


Comma<T>: Vec<T> = {
    <mut v:(<T> COMMA)*> <e:T> => {
        v.push(e);
        v
    }
};


And<T>: Vec<T> = {
    <mut v:(<T> AND)*> <e:T> => {
        v.push(e);
        v
    }
};

